buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'org.kordamp.gradle:stats-gradle-plugin:0.1.+'
  }
}

apply plugin:'groovy'
apply plugin:'distribution'
apply plugin:'org.kordamp.gradle.stats'

def projectName = 'CVM'
description = "$projectName - Configuration Versioning Manager"

group = 'com.allthingsmonitoring.utils.CVM'
archivesBaseName = 'cvm'
distsDirName = 'dist'
version = '1.0.0'

def authorString = 'Sebastian YEPES FERNANDEZ (syepes@gmail.com)'
def copyrightString = 'Copyright &#169; 2014 &#8211; Sebastian YEPES FERNANDEZ All Rights Reserved.'


defaultTasks 'clean','build','groovydoc','distZip'


// http://svn.codehaus.org/griffon/builders/jidebuilder/trunk/build.gradle
// http://gparallelizer.googlecode.com/svn/trunk/build.gradle
configure([compileGroovy, compileTestGroovy]) {
  //groovyOptions.useAnt = true
  //compileGroovy.groovyOptions.fork = true
  groovyOptions.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')
  groovyOptions.encoding = 'UTF-8'
  //groovyOptions.optimizationOptions.indy = true
}

//configurations.compile.transitive = false
compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}


repositories {
  maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
  maven { url 'http://oss.sonatype.org/content/groups/public/' }
  maven { url 'http://repository.codehaus.org/' }
  mavenLocal()
  mavenCentral()
}


// 1.0.11 (GSiftingAppender.groovy) https://github.com/qos-ch/logback/commit/a86350e20ebf79b70aa737ea6149bf8485f2d295#logback-classic/src/main/groovy/ch/qos/logback
dependencies {
  compile 'org.codehaus.groovy:groovy:2.+',
          'org.codehaus.groovy:groovy-jmx:2.+',
          'org.codehaus.groovy:groovy-json:2.+',
          'org.codehaus.groovy:groovy-xml:2.+',
          'org.codehaus.groovy:groovy-nio:2.+',
          'org.codehaus.groovy:groovy-ant:2.+',
          'commons-cli:commons-cli:1.+',
          'org.slf4j:slf4j-api:1.7.+',
          'ch.qos.logback:logback-core:1.0.11',
          'ch.qos.logback:logback-classic:1.0.11',
          'com.jcraft:jsch:0.1.+',
          'com.jcraft:jzlib:1.+',
          'org.eclipse.jgit:org.eclipse.jgit:3.+',
          'net.sf.expectit:expectit-core:0.+',
          'com.github.fge:json-schema-validator:2.+',
          fileTree(dir: 'libs', include: '*.jar')

  compile 'com.github.groovy-wslite:groovy-wslite:1.+', {
            exclude module: "groovy-all"
          }
  compile 'org.codehaus.jsr166-mirror:jsr166y:1.7.+'
  compile 'org.codehaus.gpars:gpars:1.+', {
            exclude module: "groovy-all"
          }

  testCompile "org.spockframework:spock-core:0.7-groovy-2.0-SNAPSHOT", {
    exclude module: "groovy-all"
  }
}


groovydoc {
  dependsOn classes
  groovyClasspath = configurations.compile // http://issues.gradle.org/browse/GRADLE-1391
  destinationDir  = new File(( System.getProperty( 'groovydocDir' ) ?: project.buildDir.path + '/groovydoc' ), ( project.version.contains( '-SNAPSHOT' ) ? '' : project.version ))
  use = true
  includePrivate = true

  windowTitle = "$projectName"
  docTitle = "$projectName"
  header = "$projectName - v$project.version"
  footer = copyrightString

  link 'http://static.springsource.org/spring/docs/3.1.x/javadoc-api/', 'org.springframework.'
  link 'http://download.oracle.com/javase/6/docs/api/',                 'java.'
  link 'http://groovy.codehaus.org/api/',                               'groovy.', 'org.codehaus.groovy.'
  link 'http://commons.apache.org/net/apidocs/',                        'org.apache.commons.net.'
  link 'http:/"truezip.java.net/apidocs/',                              'de.schlichtherle.'
}


processResources{
  exclude 'config.groovy'
  exclude '*.sh'
}

jar {
  version = ''
  from ( configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } ) {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
  }
  manifest {
    attributes 'Implementation-Title': project.description,
               'Implementation-Version': project.version,
               'Specification-Title': project.description,
               'Specification-Version': project.version,
               'Built-By': System.properties['user.name'],
               'Built-JDK': System.properties['java.version'] +' ('+ System.properties['java.vm.vendor'] +')',
               'Built-Date': new Date(),
               'Author': authorString,
               'Main-Class': project.group
  }
}

distributions {
    main {
        baseName = archivesBaseName
        contents {
            from { 'build/libs' }
            from { 'src/dist' }
            from { 'README.md' }
            from { 'CHANGELOG.md' }
            from { 'LICENSE.md' }
        }
    }
}
